package backend;

import lime.utils.Assets;
import openfl.utils.Assets as OpenFlAssets;
import haxe.Json;

typedef WeekFile =
{
	// JSON variables
	var songs:Array<Dynamic>;
	var weekCharacters:Array<String>;
	var weekBackground:String;
	var weekBefore:String;
	var storyName:String;
	var weekName:String;
	var startUnlocked:Bool;
	var hiddenUntilUnlocked:Bool;
	var hideStoryMode:Bool;
	var hideFreeplay:Bool;
	var difficulties:String;
	// 新增：section字段，用于分类周目
	var ?section:Null<Int>;
}

class WeekData {
	public static var weeksLoaded:Map<String, WeekData> = new Map<String, WeekData>();
	public static var weeksList:Array<String> = [];
	public var folder:String = '';

	// JSON variables
	public var songs:Array<Dynamic>;
	public var weekCharacters:Array<String>;
	public var weekBackground:String;
	public var weekBefore:String;
	public var storyName:String;
	public var weekName:String;
	public var startUnlocked:Bool;
	public var hiddenUntilUnlocked:Bool;
	public var hideStoryMode:Bool;
	public var hideFreeplay:Bool;
	public var difficulties:String;
	// 新增：section字段
	public var section:Int = 0;

	public var fileName:String;

	public static function createWeekFile():WeekFile {
		var weekFile:WeekFile = {
			songs: [["Bopeebo", "face", [146, 113, 253]], ["Fresh", "face", [146, 113, 253]], ["Dad Battle", "face", [146, 113, 253]]],
			#if BASE_GAME_FILES
			weekCharacters: ['dad', 'bf', 'gf'],
			#else
			weekCharacters: ['bf', 'bf', 'gf'],
			#end
			weekBackground: 'stage',
			weekBefore: 'tutorial',
			storyName: 'Your New Week',
			weekName: 'Custom Week',
			startUnlocked: true,
			hiddenUntilUnlocked: false,
			hideStoryMode: false,
			hideFreeplay: false,
			difficulties: '',
			section: 0 // 新增：默认section为0
		};
		return weekFile;
	}

	// HELP: Is there any way to convert a WeekFile to WeekData without having to put all variables there manually? I'm kind of a noob in haxe lmao
	public function new(weekFile:WeekFile, fileName:String) {
		// 先设置所有字段
		for (field in Reflect.fields(weekFile))
			if(Reflect.fields(this).contains(field)) // Reflect.hasField() won't fucking work :/
				Reflect.setProperty(this, field, Reflect.getProperty(weekFile, field));

		// 处理可选的section字段
		if (weekFile.section != null) {
			this.section = weekFile.section;
		} else {
			this.section = 0; // 默认值
		}

		this.fileName = fileName;
	}

	public static function reloadWeekFiles(isStoryMode:Null<Bool> = false)
	{
		weeksList = [];
		weeksLoaded.clear();
		#if MODS_ALLOWED
		var directories:Array<String> = [Paths.mods(), Paths.getSharedPath()];
		var originalLength:Int = directories.length;

		for (mod in Mods.parseList().enabled)
			directories.push(Paths.mods(mod + '/'));
		#else
		var directories:Array<String> = [Paths.getSharedPath()];
		var originalLength:Int = directories.length;
		#end

		var sexList:Array<String> = CoolUtil.coolTextFile(Paths.getSharedPath('weeks/weekList.txt'));
		for (i in 0...sexList.length) {
			for (j in 0...directories.length) {
				var fileToCheck:String = directories[j] + 'weeks/' + sexList[i] + '.json';
				if(!weeksLoaded.exists(sexList[i])) {
					var week:WeekFile = getWeekFile(fileToCheck);
					if(week != null) {
						var weekFile:WeekData = new WeekData(week, sexList[i]);

						#if MODS_ALLOWED
						if(j >= originalLength) {
							weekFile.folder = directories[j].substring(Paths.mods().length, directories[j].length-1);
						}
						#end

						if(weekFile != null && (isStoryMode == null || (isStoryMode && !weekFile.hideStoryMode) || (!isStoryMode && !weekFile.hideFreeplay))) {
							weeksLoaded.set(sexList[i], weekFile);
							weeksList.push(sexList[i]);
						}
					}
				}
			}
		}

		#if MODS_ALLOWED
		for (i in 0...directories.length) {
			var directory:String = directories[i] + 'weeks/';
			if(FileSystem.exists(directory)) {
				var listOfWeeks:Array<String> = CoolUtil.coolTextFile(directory + 'weekList.txt');
				for (daWeek in listOfWeeks)
				{
					var path:String = directory + daWeek + '.json';
					if(FileSystem.exists(path))
					{
						addWeek(daWeek, path, directories[i], i, originalLength);
					}
				}

				for (file in FileSystem.readDirectory(directory))
				{
					var path = haxe.io.Path.join([directory, file]);
					if (!FileSystem.isDirectory(path) && file.endsWith('.json'))
					{
						addWeek(file.substr(0, file.length - 5), path, directories[i], i, originalLength);
					}
				}
			}
		}
		#end
	}

	private static function addWeek(weekToCheck:String, path:String, directory:String, i:Int, originalLength:Int)
	{
		if(!weeksLoaded.exists(weekToCheck))
		{
			var week:WeekFile = getWeekFile(path);
			if(week != null)
			{
				var weekFile:WeekData = new WeekData(week, weekToCheck);
				if(i >= originalLength)
				{
					#if MODS_ALLOWED
					weekFile.folder = directory.substring(Paths.mods().length, directory.length-1);
					#end
				}
				if((PlayState.isStoryMode && !weekFile.hideStoryMode) || (!PlayState.isStoryMode && !weekFile.hideFreeplay))
				{
					weeksLoaded.set(weekToCheck, weekFile);
					weeksList.push(weekToCheck);
				}
			}
		}
	}

	private static function getWeekFile(path:String):WeekFile {
		var rawJson:String = null;
		#if MODS_ALLOWED
		if(FileSystem.exists(path)) {
			rawJson = File.getContent(path);
		}
		#else
		if(OpenFlAssets.exists(path)) {
			rawJson = Assets.getText(path);
		}
		#end

		if(rawJson != null && rawJson.length > 0) {
			return cast tjson.TJSON.parse(rawJson);
		}
		return null;
	}

	//   FUNCTIONS YOU WILL PROBABLY NEVER NEED TO USE

	//To use on PlayState.hx or Highscore stuff
	public static function getWeekFileName():String {
		return weeksList[PlayState.storyWeek];
	}

	//Used on LoadingState, nothing really too relevant
	public static function getCurrentWeek():WeekData {
		return weeksLoaded.get(weeksList[PlayState.storyWeek]);
	}

	public static function setDirectoryFromWeek(?data:WeekData = null) {
		Mods.currentModDirectory = '';
		if(data != null && data.folder != null && data.folder.length > 0) {
			Mods.currentModDirectory = data.folder;
		}
	}
	
	// 新增：获取所有唯一的section值
	public static function getAllSections():Array<Int> {
		var sections:Array<Int> = [];
		for (week in weeksLoaded) {
			if (!sections.contains(week.section)) {
				sections.push(week.section);
			}
		}
		sections.sort((a, b) -> a - b); // 升序排序
		return sections;
	}
	
	// 新增：根据section获取周目列表
	public static function getWeeksBySection(section:Int):Array<WeekData> {
		var result:Array<WeekData> = [];
		for (week in weeksLoaded) {
			if (week.section == section) {
				result.push(week);
			}
		}
		return result;
	}
	
	// 新增：获取指定周目的section
	public static function getWeekSection(weekName:String):Int {
		var week = weeksLoaded.get(weekName);
		return (week != null ) ? week.section : 0;
	}

	/**
	 * 取单曲的「音乐人 / 作曲」。
	 * week.json 的 songs 项是数组元组，扩展后为：
	 *   [歌名, 角色, 颜色, 音乐人, [各难度谱师]]
	 * 旧版 3 元组（以及没有第 4 项的）返回 null，交给上层回退。
	 */
	public static function getSongMusican(song:Array<Dynamic>):String {
		if (song == null || song.length < 4) return null;
		var value:Dynamic = song[3];
		if (value == null || !Std.isOfType(value, String)) return null;
		var str:String = cast value;
		return (str.length > 0) ? str : null;
	}

	/**
	 * 取单曲的「各难度谱师」数组（按难度顺序）。
	 * 既支持 ["A","B","C"] 数组形式，也支持 {normal:"A", hard:"B"} 映射形式（后者由 SongMetaConfig 处理）。
	 */
	public static function getSongCharters(song:Array<Dynamic>):Array<String> {
		if (song == null || song.length < 5) return null;
		var value:Dynamic = song[4];
		if (value == null || !Std.isOfType(value, Array)) return null;
		var list:Array<Dynamic> = cast value;
		var out:Array<String> = [];
		for (entry in list)
			out.push(entry == null ? "" : Std.string(entry));
		return (out.length > 0) ? out : null;
	}

	/**
	 * 在指定周目里按歌名找元组条目（用于取音乐人 / 谱师）。
	 */
	public static function findSongEntry(?data:WeekData = null, songName:String):Array<Dynamic> {
		if (songName == null || songName.length == 0) return null;
		var week:WeekData = (data != null) ? data : getCurrentWeek();
		if (week == null || week.songs == null) return null;

		for (entry in week.songs)
		{
			if (entry == null || !Std.isOfType(entry, Array)) continue;
			var tuple:Array<Dynamic> = cast entry;
			if (tuple.length > 0 && Std.string(tuple[0]) == songName)
				return tuple;
		}
		return null;
	}
}